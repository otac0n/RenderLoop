@namespace RenderLoop.Demo.MGS.Codec.Conversation
@classname ConversationParser
@accessibility internal
@using System.Linq
@start conversation

conversation <IList<Response>> = r:response* EOF { r }

response <Response> -export = characterResponse / codeResponse

characterResponse <CharacterResponse>
    = n:("" name<1,2,WS>) _ m:mood _ ":" _ t:text EOL? { new CharacterResponse(n, m, t) }
    / n:("" name<1,2,WS>) _ ":" _ m:mood? _ t:text EOL? { new CharacterResponse(n, m.SingleOrDefault(), t) }

name = "" [^\[\]:` \t\n]+

mood = "[" m:("" [^\]]+) "]" { m }

text = "" (!codeStart [^\n])*

codeResponse <CodeResponse>
    = "```" lang? EOL code:("" (!"```" .)*) "```"? EOL? { new CodeResponse(code) }
    / "[code]" code:("" (!"[/code]" .)*) "[/code]?" EOL? { new CodeResponse(code) }

codeStart = ("```" lang? EOL) / "[code]"

lang = [a-z]+

WS = "" [ \t]+

EOL = "" "\n"*

_ = "" WS?

EOF = !. / #ERROR{ "Could not parse response." }
